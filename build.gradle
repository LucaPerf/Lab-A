import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

//Use application and OpenJFX plugin
plugins
        {
            id 'application'
            id 'org.openjfx.javafxplugin' version '0.0.10'
            id "com.github.johnrengelman.shadow" version "7.0.0"
        }
//Current OS
OperatingSystem os = DefaultNativePlatform.currentOperatingSystem
//Where the class with the main method is
mainClassName = 'com.lab.centrivaccinali.Launcher'
//Needed to fix JFoenix TextField errors
applicationDefaultJvmArgs = ["--add-opens=java.base/java.lang.reflect=ALL-UNNAMED"]
//Maven repository to download dependencies from
repositories
        {
            mavenCentral()
        }
//Dependencies
dependencies
        {
            implementation 'com.jfoenix:jfoenix:9.0.10'
            implementation group: 'org.simpleflatmapper', name: 'sfm-csv', version: '8.2.3'
            implementation group: 'org.simpleflatmapper', name: 'lightning-csv', version: '8.2.3'

        }
//OpenJFX version and modules to use in the project
javafx {
    version = "16"
    modules = ['javafx.controls', 'javafx.fxml']
}

//Adds the proper zip package name
shadowDistZip
        {
            destinationDirectory = file("$rootDir/bin")
            archiveBaseName.set("Lab-AB")
            String classifier
            if (os.isLinux())
                classifier = 'linux'
            else if (os.isWindows())
                classifier = 'windows'
            else
                classifier = 'macOs'
            archiveClassifier.set(classifier)
        }

//Delete unneeded starter (.sh and .bat) scripts as OpenJFX is bundled in the jar and is platform specific. Per-platform builds are required.
startShadowScripts
        {
            doLast {
                if (os.isWindows())
                    delete unixScript
                else
                    delete windowsScript
            }
        }